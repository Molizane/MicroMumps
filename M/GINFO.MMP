ginfo	; Shows information about a GLOBALS.DAT file
	; by Marcelo Dantas
	s f=4
	s dr=$zg
	w !,"Information about ",dr,":GLOBALS.DAT"
	o f:(dr:"GLOBALS.DAT":8)
	;
	u f:0 r *x u 0 i x=255 w !,"Fresh file."
	u f r *d1,*d2 u 0 w !,"First directory block: ",(d2*256)+d1
	u f r *b1,*b2 u 0 w !,"First allocated block: ",(b2*256)+b1
	u f r *b1,*b2 u 0 w !,"Number of free data blocks: ",(b2*256)+b1
	;
	s bpl=50 ; Blocks per line
	s bp=0 ; Blocks printed
	s cb=0 ; Current block
	w !,"Block map"
	w !,"      " f i=1:1:bpl w $s(i-1#5:".",1:"!")
	d pblock
nxt	u f r *a u 0 d decode
	g nxt:cb<1000
	w ! q:(d2*256)+d1=65535
	d dirblk
	q
z	zs ginfo zp ginfo q
	; ----------------------------------------------------
decode	; Decodes a block bitmap
	s b="" f i=1:1:8 s b=a#2_b,a=a\2
	f i=1:1:8 d decode2
	q
decode2	;
	s c=$e(b,i) w $s(c:" ",1:"*")
	s cb=cb+1,bp=bp+1
	i bp=bpl s bp=0 d pblock
	q
pblock	;
	w !,$j(cb,5)," "
	q
	; ----------------------------------------------------
dirblk	; Prints the contents of the directory blocks
	s db=(d2*256)+d1
	s sector=db*6
	s cc=0
	u f:sector r *count u 0 s cc=cc+1
	w !,"----------------------------------------"
	w !,"Directory block "_db_" contains "_count_" entries.",!
	s col=0
	f i=1:1:count d dirblk2 s cc=cc+12,col=col+16 w ?col i col>50 s col=0 w !
	f i=cc:1:765 u f r *a u 0
	u f r *d1,*d2 u 0 
	g:d2<10 dirblk
	q
dirblk2	;
	s n="^"
	u f r *size u 0
	f j=1:1:size u f r *a u 0 s n=n_$c(a)
	f k=j:1:8 u f r *a u 0
	u f r *b1,*b2 u 0 s blk=(b2*256)+b1
	s n=n_"("_blk_") "
	w n,$e("                ",1,16-$l(n))
	q
